import groovy.util.slurpersupport.GPathResult
import groovy.xml.XmlUtil

task prettyXml {

    File inputXml = file('input.xml')
    File outputXml = file('output.xml')

    inputs.files inputXml
    outputs.files outputXml

    doLast {

        outputXml.withOutputStream { outputStream ->
            GPathResult result = new XmlSlurper().parse(inputXml)
            XmlUtil.serialize(result, outputStream)
        }
    }
}

task readme {
    File version = file('version')
    File template = file('readme.txt.template')
    def readmeInputs = files(version, template)

    File readme = file("readme.txt")

    inputs.files readmeInputs
    outputs.files readme

    doLast {
        readme.delete()
        readme.createNewFile()
        readme.withWriter { BufferedWriter writer ->
            writer << version.newInputStream()
            writer.newLine()
            writer.newLine()
            writer << template.newInputStream()
        }
    }
}

